plugins {
  id 'fabric-loom' version '0.7.30'
}

group = 'dev.sapphic'
version = '1.0.0'

loom {
  accessWidener = file '.accesswidener'
  refmapName = 'mixins/wearablebackpacks/refmap.json'
}

dependencies {
  minecraft 'com.mojang:minecraft:1.16.5'
  mappings 'net.fabricmc:yarn:1.16.5+build.9:v2'
  modImplementation 'net.fabricmc:fabric-loader:0.11.3'

  implementation 'com.google.code.findbugs:jsr305:3.0.2'
  implementation 'org.jetbrains:annotations:21.0.1'
  implementation 'org.checkerframework:checker-qual:3.13.0'
  
  final modInclude = { modImplementation include(it) }
  final fabricApi = 'net.fabricmc.fabric-api:fabric'

  modInclude "$fabricApi-api-base:0.3.0+c88702897d"
  modInclude "$fabricApi-events-interaction-v0:0.4.2+ca58154a7d"
  modInclude "$fabricApi-key-binding-api-v1:1.0.4+9354966b7d"
  modInclude "$fabricApi-lifecycle-events-v1:1.2.1+ca58154a7d"
  modInclude "$fabricApi-networking-api-v1:1.0.3+e3c9d0627d"
  modInclude "$fabricApi-networking-blockentity-v0:0.2.8+ca58154a7d"
  modInclude "$fabricApi-object-builder-api-v1:1.9.5+c88702897d"
  modInclude "$fabricApi-registry-sync-v0:0.7.4+ca58154a7d"
  modInclude "$fabricApi-renderer-registries-v1:2.3.0+2868a2287d"
  modInclude "$fabricApi-rendering-v1:1.6.0+2868a2287d"
  modInclude "$fabricApi-resource-loader-v0:0.4.6+2868a2287d"
  modInclude "$fabricApi-screen-handler-api-v1:1.1.5+9354966b7d"
}

compileJava {
  options.with {
    it.release.set(8)
    it.fork = true
    it.deprecation = true
    it.encoding = 'UTF-8'
    it.compilerArgs = ['-Xlint:all', '-parameters']
  }
}

processResources {
  filesMatching('/fabric.mod.json') {
    expand version: version
  }
}

jar {
  from '/LICENSE'
  manifest.attributes(
    'Specification-Title': project.group,
    'Specification-Vendor': project.group,
    'Specification-Version': '1.0.0',
    'Implementation-Title': project.name,
    'Implementation-Version': project.version,
    'Implementation-Vendor': project.group,
    'Sealed': 'true'
  )
}
